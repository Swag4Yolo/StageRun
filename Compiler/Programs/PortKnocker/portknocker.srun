# Ports
PIN  fromExternal
PIN  fromInternal
POUT toExternal
POUT toInternal

# Resources Allocation
HASH ipv4_hash {IPV4.SRC}

# State
REG knockStatus
REG acceptedConnections
VAR status
VAR isAccepted

# Stage 1: SSH access (only afater full knock)
HANDLER ssh_access
  KEY PKT.PORT == fromExternal
  KEY L4.DPORT == 22
  DEFAULT FWD toInternal
  begin:
    MGET acceptedConnections[IPV4.SRC] -> isAccepted
    IF isAccepted == 0:
      DROP
    ENDIF
END

# Stage 2: Knock step 1
HANDLER knock_step1
  KEY PKT.PORT == fromExternal
  KEY L4.DPORT == 1234
  begin:
    MSET knockStatus[IPV4.SRC] 1
END

# Stage 3: Knock step 2 (requires step 1)
HANDLER knock_step2
  KEY PKT.PORT == fromExternal
  KEY L4.DPORT == 5678
begin:
  MGET knockStatus[IPV4.SRC] -> status
  IF status == 1:
    MSET knockStatus[IPV4.SRC] 2
  ELSE:
    MSET knockStatus[IPV4.SRC] 0
  ENDIF
END

# Stage 4: Knock step 3 (requires step 2) -> grant access
HANDLER knock_step3
  KEY PKT.PORT == fromExternal
  KEY L4.DPORT == 9012
begin:
  MGET knockStatus[IPV4.SRC] -> status
  IF status == 2:
    MSET acceptedConnections[IPV4.SRC] 1
  ELSE:
    MSET knockStatus[IPV4.SRC] 0
  ENDIF
END

# Stage 5: Other inbound traffic (default path)
HANDLER from_external_default
  KEY PKT.PORT == fromExternal
  DEFAULT FWD toInternal
END

# Outbound: plain forwarding
HANDLER from_internal
  KEY PKT.PORT == fromInternal
  DEFAULT FWD toExternal
END