// stagerun_grammar.lark

start: NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

?statement: port_in_decl
          | port_out_decl
          | qset_decl
          | var_decl
          | reg_decl
          | hash_decl
          | handler

// PORTS
// port_declr: "PORT" NAME
port_in_decl: "PIN" NAME
port_out_decl: "POUT" NAME NAME?

// QUEUES
qset_decl: "QSET" NAME NAME INT

// Resources
var_decl: "VAR" NAME
reg_decl: "REG" NAME
hash_decl: "HASH" NAME "{" (header_ref | INT) ("," (header_ref | INT))* "}"

handler: "HANDLER" NAME NEWLINE+ clause_line* "END"

?clause_line: key_clause NEWLINE+
            | default_clause NEWLINE+
            | body_clause

key_clause: "KEY" key_ref (comp_op | mask_op) value
default_clause: "DEFAULT" default_instr

body_clause: label_decl NEWLINE+ instr_line+ NEWLINE*
instr_line: instr NEWLINE

?default_instr: default_fwd_instr
              | default_drop_instr
              | default_fwd_queue_instr

default_fwd_instr: "FWD" NAME
default_fwd_queue_instr: "ENQUEUE" NAME SIGNED_INT
default_drop_instr: "DROP"
// default_rts_instr: "RTS"


      // Fwd Ops      
?instr: fwd_instr
      | fwd_queue_instr
      | drop_instr
      | rts_instr
      // Arith Instructions
      | sum_instr
      | sub_instr
      | mul_instr
      | hinc_instr
      | header_assign_instr
      | clone_instr
      | paddtern_instr
      | time_instr
      // Mem Ops
      | minc_instr
      | mget_instr
      | mset_instr
      // Convert ops
      | copy_header_to_var_instr
      | copy_hash_to_var_instr
      | copy_var_to_header_instr
      | activate_instr
      | ingress_ts_instr
      // | airth_instr
      | hairth_instr
      | random_instr
      | in_instr
      | out_instr
      | conditional_clause


// Instructions List
fwd_instr: ".fwd" NAME
fwd_queue_instr: ".fwd.enq" NAME SIGNED_INT
drop_instr: ".drop"
rts_instr: ".rts"

hinc_instr: ".hinc" header_ref "," SIGNED_INT
header_assign_instr: ".hassign" header_ref "," (SIGNED_INT | STRING)
clone_instr: ".clone" NAME
paddtern_instr: ".pad.pattern" INT+
activate_instr: ".activate" NAME
ingress_ts_instr: ".igts" "->" var_ref
random_instr: ".rand" SIGNED_INT "," var_ref
time_instr: ".time" "," var_ref

// Arithmetic Operations
  // Headers
hairth_instr: ".harit" header_ref arith_op (header_ref | SIGNED_INT) "->" header_ref
  // Vars
// airth_instr: ".arit" var_ref arith_op (var_ref | SIGNED_INT) "->" var_ref
sum_instr: ".sum" var_ref "," var_ref "," var_ref
sub_instr: ".sub" var_ref "," var_ref "," var_ref
mul_instr: ".mul" var_ref "," SIGNED_INT "," var_ref

// Memory Instructions
minc_instr: ".minc" reg_ref "[" (header_ref | hash_ref) "]" (header_ref | SIGNED_INT) "," var_ref ("(" NAME ")")?
mget_instr: ".mget" reg_ref "[" (header_ref | hash_ref) "]" "," var_ref ("(" NAME ")")?
mset_instr: ".mset" reg_ref "[" (header_ref | hash_ref) "]" "," (header_ref | var_ref | SIGNED_INT)

// Convert Instrs
copy_header_to_var_instr: ".hcopy" header_ref "," var_ref
copy_hash_to_var_instr: ".hashcopy" hash_ref "," var_ref
copy_var_to_header_instr: ".copy" var_ref "," header_ref

// State Instrs
in_instr: "IN" var_ref
out_instr: "OUT" var_ref

// Conditional Instrs
//TODO: delete
if_block: if_clause elif_clause* else_clause? "ENDIF"
if_clause: "IF" bool_expr ":" instr+
elif_clause: "ELIF" bool_expr ":" instr+
else_clause: "ELSE" ":" instr+

conditional_clause: ".br.cond" bool_expr "," label

// =====================
// Expressões booleanas SEM headers
//   - var_ref: NAME simples (p.ex., proto, size)
//   - constantes: inteiros (suporta sinal)
//   - operadores: &&, ||, !, parêntesis, comparações
// =====================
?bool_expr: or_expr

?or_expr: and_expr
        | or_expr "||" and_expr

?and_expr: not_expr
         | and_expr "&&" not_expr

?not_expr: comparison
         | "!" not_expr
         | "(" bool_expr ")"

?comparison: arith_val comp_op arith_val
           | "(" comparison ")"

EQ: "=="
NE: "!="
LT: "<"
LE: "<="
GT: ">"
GE: ">="

comp_op: EQ | NE | LT | LE | GT | GE

MASK: "&"
mask_op: MASK

?arith_val: var_ref
          | SIGNED_INT
          | "(" arith_val ")"


// Arithmetic Ops
ARIT_OR: "|"
ARIT_AND: "&"
ARIT_SUM: "+"
ARIT_SUB: "-"
ARIT_MUL: "*"

arith_op: ARIT_OR | ARIT_AND | ARIT_SUM | ARIT_SUB | ARIT_MUL

// Atenção: var_ref é APENAS NAME (sem ponto); nada de headers aqui.



key_ref:  NAME "." NAME
header_ref: NAME "." NAME
var_ref: "$"NAME
reg_ref: NAME
hash_ref: NAME
value: NAME | STRING | SIGNED_INT

// Label
label_decl: NAME ":"
label: NAME

// IPV4_ADDR: /(?:(?:25[0-5]|2[0-4]\d|1?\d?\d)(?:\.(?:25[0-5]|2[0-4]\d|1?\d?\d)){3})/
NAME: /[A-Za-z_][A-Za-z0-9_]*/
STRING: /"[^"]*"/
LINE_COMMENT: /#[^\n]*/
BLOCK_COMMENT: /\/\*[\s\S]*?\*\//
%ignore LINE_COMMENT
%ignore BLOCK_COMMENT
%import common.INT
%import common.SIGNED_INT
%import common.NEWLINE
%import common.WS_INLINE
%ignore WS_INLINE
// %import common.WS
// %ignore WS
// %ignore /#[^\n]*/ // Language Comments
// %ignore /[\r\n]+/
